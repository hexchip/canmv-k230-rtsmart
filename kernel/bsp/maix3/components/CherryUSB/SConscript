from building import *

cwd = GetCurrentDir()

path = [cwd]
path += [cwd + '/common']
path += [cwd + '/core']
path += [cwd + '/class/cdc']
path += [cwd + '/class/msc']
path += [cwd + '/class/hid']
path += [cwd + '/class/audio']
path += [cwd + '/class/video']
path += [cwd + '/class/wireless']
path += [cwd + '/class/dfu']
path += [cwd + '/class/midi']
path += [cwd + '/class/vendor/net']
path += [cwd + '/class/vendor/serial']
path += [cwd + '/class/adb']

src = []

CPPDEFINES = []
LOCAL_CCFLAGS = []

if GetDepend(['CHERRY_USB_OSAL_RTTHREAD']):
    path += [cwd + '/osal']
    src += Glob('osal/usb_osal_rtthread.c')

if GetDepend(['CHERRY_USB_SPEED_HS']):
    CPPDEFINES+=['CONFIG_USB_HS']

# USB DEVICE
if GetDepend(['ENABLE_CHERRY_USB_DEVICE']):
    src += Glob('core/usbd_core.c')

    if GetDepend(['CHERRYUSB_DEVICE_DWC2_K230']):
        src += Glob('port/dwc2/usb_dc_dwc2.c')

    if GetDepend(['CHERRY_USB_DEVICE_ENABLE_CLASS_CDC_ACM']):
        src += Glob('class/cdc/usbd_cdc.c')

    if GetDepend(['CHERRY_USB_DEVICE_ENABLE_CLASS_MSC']):
        src += Glob('class/msc/usbd_msc.c')

    if GetDepend(['CHERRY_USB_DEVICE_ENABLE_CLASS_MTP']):
        path += [cwd + '/class/mtp']
        path += [cwd + '/class/mtp/uMTP-Responder/inc']
        src += Glob('class/mtp/usbd_mtp.c')
        src += Glob('class/mtp/uMTP-Responder/src/*.c')
        src += Glob('class/mtp/uMTP-Responder/src/mtp_operations/*.c')

    if GetDepend(['CHERRY_USB_DEVICE_ENABLE_CLASS_UVC']):
        src += Glob('class/video/usbd_video.c')

    if GetDepend(['CHERRY_USB_DEVICE_ENABLE_CLASS_ADB']):
        src += Glob('class/adb/usbd_adb.c')

if GetDepend(['ENABLE_CHERRY_USB_HOST']):
    src += Glob('core/usbh_core.c')

    path += [cwd + '/class/hub/']
    src += Glob('class/hub/usbh_hub.c')

    if GetDepend(['CHERRYUSB_HOST_DWC2_K230']):
        if GetDepend(['CHERRY_USB_HC_DRV_ORG']):
            src += Glob('port/dwc2/usb_hc_dwc2.c')
        if GetDepend(['CHERRY_USB_HC_DRV_DWC2']):
            src += Glob('port/dwc2_linux/hcd.c')
            src += Glob('port/dwc2_linux/core.c')
            src += Glob('port/dwc2_linux/core_intr.c')
            src += Glob('port/dwc2_linux/hcd_ddma.c')
            src += Glob('port/dwc2_linux/hcd_intr.c')
            src += Glob('port/dwc2_linux/hcd_queue.c')
            src += Glob('port/dwc2_linux/params.c')
            src += Glob('port/dwc2_linux/dwc2_dep.c')

    if GetDepend(['CHERRY_USB_HOST_ENABLE_CLASS_HID']):
        path += [cwd + '/class/hid/']
        src += Glob('class/hid/usbh_hid.c')

    if GetDepend(['CHERRY_USB_HOST_ENABLE_CLASS_MSC']):
        path += [cwd + '/class/msc/']
        src += Glob('class/msc/usbh_msc.c')

    if GetDepend(['CHERRY_USB_HOST_ENABLE_CLASS_UVC']):
        path += [cwd + '/class/video/']
        src += Glob('class/video/usbh_video.c')

    if GetDepend(['CHERRY_USB_HOST_ENABLE_CLASS_NET_RTL8152']):
        src += Glob('class/vendor/net/usbh_rtl8152.c')

    if GetDepend(['CHERRY_USB_HOST_ENABLE_CLASS_SERIAL']):
        if GetDepend(['CHERRY_USB_HOST_ENABLE_CLASS_SERIAL_EC200M']):
            src += Glob('class/vendor/serial/usbh_ec200m.c')
        if GetDepend(['CHERRY_USB_HOST_ENABLE_CLASS_SERIAL_CH34x']):
            src += Glob('class/vendor/serial/usbh_ch34x.c')
        if GetDepend(['CHERRY_USB_HOST_ENABLE_CLASS_SERIAL_CP210x']):
            src += Glob('class/vendor/serial/usbh_cp210x.c')
        if GetDepend(['CHERRY_USB_HOST_ENABLE_CLASS_SERIAL_FTDI']):
            src += Glob('class/vendor/serial/usbh_ftdi.c')

    if GetDepend(['CHERRY_USB_HOST_ENABLE_CLASS_CDC_ACM']):
        src += Glob('class/cdc/usbh_cdc_acm.c')

    if GetDepend(['CHERRY_USB_HOST_ENABLE_CLASS_CDC_ECM']):
        src += Glob('class/cdc/usbh_cdc_ecm.c')

if GetDepend(['CHERRYUSB_HOST_DWC2_K230']) or GetDepend(['CHERRYUSB_DEVICE_DWC2_K230']):
    src += Glob('port/dwc2/usb_glue_kendryte.c')

group = DefineGroup('CherryUSB', src, depend = ['ENABLE_CHERRY_USB'], CPPPATH = path, CPPDEFINES = CPPDEFINES)

Return('group')
