from building import *
import rtconfig
import os

src = []
inc = []
group = []

cwd = GetCurrentDir() # get current dir path

port_src = Glob('*.c')
port_inc = [cwd]

mpp_src_dir = os.getenv('MPP_SRC_DIR')
LIBPATH = []
LINKFLAGS = ''
if mpp_src_dir is not None:
    port_inc += [mpp_src_dir + '/include/comm', mpp_src_dir + '/include/ioctl' ,mpp_src_dir + '/userapps/api']
    port_src += [mpp_src_dir + '/userapps/src/connector/mpi_connector.c']
    userapps_lib_dir = cwd + '/lib'

    if os.path.exists(userapps_lib_dir):
        LIBPATH += [userapps_lib_dir]

        libname = ''
        liblist = os.listdir(userapps_lib_dir)
        for item in liblist:
            if item[-2:] == ".a":
                libname = libname + '-l' + item[3:-2] + ' '

        LINKFLAGS += ' -Wl,--whole-archive ' + libname  + ' -Wl,--no-whole-archive'

group = group + DefineGroup('LVGL-port', port_src, depend = ['PKG_USING_LVGL'], CPPPATH = port_inc, LIBPATH = LIBPATH, LINKFLAGS = LINKFLAGS)

# check if .h or .hpp files exists
def check_h_hpp_exists(path):
    file_dirs = os.listdir(path)
    for file_dir in file_dirs:
        if os.path.splitext(file_dir)[1] in ['.h', '.hpp']:
            return True
    return False

lvgl_cwd = cwd + '/lvgl/'

lvgl_src_cwd = lvgl_cwd + 'src/'
inc = inc + [lvgl_src_cwd]
src = src + Glob(os.path.join(lvgl_src_cwd,'*.c'))
for root, dirs, files in os.walk(lvgl_src_cwd):
    for dir in dirs:
        current_path = os.path.join(root, dir)
        if current_path == os.path.join(lvgl_src_cwd, 'libs', 'thorvg', 'rapidjson', 'msinttypes'): # exclude the msinttypes folder
            continue
        src = src + Glob(os.path.join(current_path,'*.c')) # add all .c files
        if check_h_hpp_exists(current_path): # add .h and .hpp path
            inc = inc + [current_path]


if GetDepend('PKG_LVGL_USING_EXAMPLES'):
    lvgl_src_cwd = lvgl_cwd + 'examples/'
    inc = inc + [lvgl_src_cwd]
    for root, dirs, files in os.walk(lvgl_src_cwd):
        for dir in dirs:
            current_path = os.path.join(root, dir)
            src = src + Glob(os.path.join(current_path,'*.c'))
            if check_h_hpp_exists(current_path):
                inc = inc + [current_path]

if GetDepend('PKG_LVGL_USING_DEMOS'):
    lvgl_src_cwd = lvgl_cwd + 'demos/'
    inc = inc + [lvgl_src_cwd]
    for root, dirs, files in os.walk(lvgl_src_cwd):
        for dir in dirs:
            current_path = os.path.join(root, dir)
            src = src + Glob(os.path.join(current_path,'*.c'))
            if check_h_hpp_exists(current_path):
                inc = inc + [current_path]

LOCAL_CFLAGS = ''
if rtconfig.PLATFORM == 'gcc' or rtconfig.PLATFORM == 'armclang': # GCC or Keil AC6
    LOCAL_CFLAGS += ' -std=c99'
elif rtconfig.PLATFORM == 'armcc': # Keil AC5
    LOCAL_CFLAGS += ' --c99 --gnu'

group = group + DefineGroup('LVGL', src, depend = ['PKG_USING_LVGL'], CPPPATH = inc, LOCAL_CFLAGS = LOCAL_CFLAGS)

Return('group')